defaultTasks 'check'

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.dorongold.task-tree'

subprojects {
    if (!(it.name in ['server','test'])) {
        // Investigate why the java plugin is needed :/
        apply plugin: 'java'
        apply plugin: 'ca.cutterslade.analyze'
    }
    apply from: "$rootProject.projectDir/dependencies.gradle"
    repositories {
        jcenter()
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:all" << "-Werror"
        }
    }
    configurations {
        all*.exclude group: 'org.glassfish.hk2.external', module: 'javax.inject'
    }
}

buildscript {
    apply from: "$rootProject.projectDir/dependencies.gradle"
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:$rootProject.gradleVersionsPluginVersion"
        classpath "ca.cutterslade.gradle:gradle-dependency-analyze:$rootProject.gradleDependencyAnalyzePluginVersion"
        classpath 'gradle.plugin.com.dorongold.plugins:task-tree:1.3'
    }
}
